buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {

        }
    }

    buildTypes {
        release {
            debuggable false
            zipAlign true
            signingConfig signingConfigs.release
        }
    }

    // Read our private signing details from the local properties file
    if (project.hasProperty('storeFile') &&
            project.hasProperty('storePassword') &&
            project.hasProperty('keyAlias') &&
            project.hasProperty('keyPassword')) {
        println 'gradle.properties OK'
        android.signingConfigs.release.storeFile = file(storeFile)
        android.signingConfigs.release.storePassword = storePassword
        android.signingConfigs.release.keyAlias = keyAlias
        android.signingConfigs.release.keyPassword = keyPassword
    } else {
        println 'gradle.properties not well formed'
        if(!project.hasProperty('storeFile')) println 'storeFile not defined'
        if(!project.hasProperty('storePassword')) println 'storePassword not defined'
        if(!project.hasProperty('keyAlias')) println 'keyAlias not defined'
        if(!project.hasProperty('keyPassword')) println 'keyPassword not defined'

        buildTypes.release.signingConfig = null
    }
}

dependencies {
    // Support Libraries
    compile 'com.android.support:appcompat-v7:+'
    compile 'com.android.support:support-v13:+'
    compile 'com.google.android.gms:play-services:4.+'
    compile 'com.google.guava:guava:15.0'
    compile 'com.jakewharton:butterknife:4.+'

    compile 'de.keyboardsurfer.android.widget:crouton:1.8.2'
}
